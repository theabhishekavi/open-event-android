plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'

def STRIPE_API_TOKEN = System.getenv('STRIPE_API_TOKEN') ?: "YOUR_API_KEY"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.eventyay.attendee"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 3
        versionName "0.0.3a"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        manifestPlaceholders = [
                STRIPE_API_TOKEN: STRIPE_API_TOKEN
        ]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://api.eventyay.com/v1/"'
        }
        debug {
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://open-event-api-dev.herokuapp.com/v1/"'
        }
    }

    flavorDimensions "default"

    productFlavors {
        fdroid {
            dimension "default"
        }

        playStore {
            dimension "default"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        pickFirst 'kotlin/**'
    }
}

spotless {
    kotlin {
        ktlint().userData(['android': 'true', 'color': 'true',
                           'max_line_length': '120', 'reporter': 'checkstyle'])
        target '**/*.kt'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    def lifecycle_version = "2.1.0-alpha02"
    def koin_version = "1.0.2"
    def roomVersion = '2.1.0-alpha04'
    def ktx_version = "1.0.0"
    def ktx2_version = "2.0.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha02'
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation "androidx.browser:browser:1.0.0"
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:${lifecycle_version}"
    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.room:room-rxjava2:${roomVersion}"
    kapt "androidx.room:room-compiler:${roomVersion}"
    testImplementation "androidx.room:room-testing:${roomVersion}"
    implementation "com.takisoft.fix:preference-v7:28.0.0.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // KTX
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.fragment:fragment-ktx:$ktx_version"
    implementation "androidx.collection:collection-ktx:$ktx_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$ktx2_version"

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // Location Play Service
    playStoreImplementation 'com.google.android.gms:play-services-location:16.0.0'

    //Smart Auth Play Service
    playStoreImplementation 'com.google.android.gms:play-services-auth:16.0.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"
    implementation 'com.github.jasminb:jsonapi-converter:0.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.13.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.5.0'

    // Cards Shimmer Animation
    implementation 'com.facebook.shimmer:shimmer:0.4.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Stripe
    implementation 'com.stripe:stripe-android:8.3.0'

    // QR Code
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    //Navigation
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-beta01'
    implementation 'android.arch.navigation:navigation-ui:1.0.0-alpha11'

    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.9"
    testImplementation 'org.threeten:threetenbp:1.3.8'
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
